; *****************************************************************************
; *****************************************************************************
;
;		Name :		core.asm
;		Purpose :	Core Routines - Next, Enter, Exit, Literals
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		6th January 2020
;
; *****************************************************************************
; *****************************************************************************

; *****************************************************************************
;
;		This sets up the "Next" code which uses self modifying code at $00
;						YX contains the first word to run.
;
; *****************************************************************************

InitialiseCoreCode:
		lda 	#$7C 					; Copy JMP ($aaaa,x) 	6 cycles when run
		sta 	NextCode+2 
		stx 	NextCode+3 				; set the indirect address (IP)
		sty 	NextCode+4

		lda 	#$4C 					; JMP AltHandler
		sta 	AltCode
		lda 	#AlternateHandler & $FF
		sta 	AltCode+1
		lda 	#AlternateHandler >> 8
		sta 	AltCode+2

		jsr 	SetRunMode 				; set run mode to current

		ldx 	#1 						; set up to run from provided word.
		rts								; (2 INX skip offset and line#)

; *****************************************************************************
;
;								Alternate handler
;
; *****************************************************************************

SetRunMode:
		lda 	FastMode
		beq 	_SRMSlow
		lda 	#$E8 					; Copy INX INX 			4 cycles when run
		sta 	NextCode
		sta 	NextCode+1
		rts
_SRMSlow:
		lda 	#$80	 				; BRA AltCode
		sta 	NextCode
		lda 	#AltCode-NextCode-2
		sta 	NextCode+1
		rts

AlternateHandler:
		inx 							; patched in - does the INX that is overwritten
		inx
		stx 	temp1 					; save it
		tsx 							; check underflow
		cpx 	#$81
		bcs 	_AHStackError
		ldx 	temp1
		lda 	BreakTestCount 			; do a break test say 1 in 32
		clc
		adc 	#8
		sta 	BreakTestCount
		bcc 	_AHNoTest
		jsr 	ExternCheckBreak
_AHNoTest:
		jmp 	NextCode+2				; then the indirect jump.
_AHStackError:
		rerror 	"STACK UNDERFLOW"

SetSlowMode: 	;; slow <0>
		lda 	#0
		bra 	SetMode
SetFastMode: 	;; fast <0>
		lda 	#1
SetMode:		
		sta 	FastMode
		jsr 	SetRunMode
		doNext


; *****************************************************************************
;
;				Word that loads a 2 byte literal onto the stack.
;
; *****************************************************************************

HexLiteral2Byte: ;; $$hexliteral noexec <2>
		nop 							; must have different address for decoding.
Literal2Byte:	;; $$literal noexec <2>
		pushTOS							; push the old TOS on the stack
		inx  							; point X to the word 
		inx 
		txa 							; copy into Y
		tay

		lda 	(IP),y 					; read it. the LSB
		sta 	TOS
		iny 							; read and push the MSB
		lda 	(IP),y
		sta 	TOS+1
		doNext

HexLiteral2ByteDecode: ;; $$hexliteral decode
		lda 	#CTH_Keyword
		jsr 	DecodeWriteBuffer
		lda 	#"$"
		jsr 	DecodeWriteBuffer
		lda 	(srcPtr),y
		pha
		iny
		lda 	(srcPtr),y
		iny
		jsr 	HL2BDWrite
		pla
		jsr 	HL2BDWrite
		rts

HL2BDWrite:
		pha
		lsr 	a
		lsr 	a
		lsr 	a
		lsr 	a
		jsr 	_HLNibble
		pla
_HLNibble:
		and 	#$0F
		cmp 	#$0A
		bcc 	_HLNoAdjust
		clc
		adc 	#7
_HLNoAdjust:
		clc
		adc 	#48
		jsr 	DecodeWriteBuffer
		rts

Literal2ByteDecode: ;; $$literal decode
		lda 	#CTH_Keyword
		jsr 	DecodeWriteBuffer
		lda 	(srcPtr),y
		tax
		iny
		lda 	(srcPtr),y
		tay
		txa
		sec 							; output signed
		jsr 	DecodeYAToBuffer		
		rts

; *****************************************************************************
;
;				Word that loads a string address onto the stack
;
; *****************************************************************************

LiteralString: ;; $$string noexec <n>
		pushTOS 						; save old TOS
		inx
		inx 							; skip over current word
		txa 							; add to IP + 1 to give string address
		tay 							; put in Y
		sec 							; make that TOS
		adc 	IP
		sta 	TOS
		lda 	IP+1
		adc 	#0
		sta 	TOS+1
		;
		txa 							; add data length to X
		clc
		adc 	(IP),y
		tax
		dex
		dex
		doNext							; jump

LiteralStringDecoder: ;; $$string decode
		lda 	#CTH_String
		jsr 	DecodeWriteBuffer
		lda 	#'"'
		jsr 	DecodeWriteBuffer
		jsr 	DecodeOutputData
		jsr 	DecodeWriteBuffer
		rts
