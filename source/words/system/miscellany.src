; *****************************************************************************
; *****************************************************************************
;
;		Name :		miscellany.src
;		Purpose :	Miscellaneous words
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		6th January 2020
;
; *****************************************************************************
; *****************************************************************************

RunProgram: 	;; run <0>
		resetStack
		jsr 	ClearVariableSpace 			; clear variables etc.
		ldx		#ProgramMemory & $FF		; boot address
		ldy 	#ProgramMemory >>8
		jsr 	InitialiseCoreCode 			; initialise the NEXT routine at $00
		doNext

; *****************************************************************************
;
;						Call a machine code routine
;
; *****************************************************************************

Call6502: 	;; sys <0>
		lda 	TOS 						; copy call address
		sta 	temp1
		lda 	TOS+1
		sta 	temp1+1
		popTOS
		phx
		lda		('A'-'@')*2+azVariables 	; load AXY
		ldx		('X'-'@')*2+azVariables 	
		ldy		('Y'-'@')*2+azVariables 	
		jsr 	_CallTemp1 					; call it		
		plx
		doNext
_CallTemp1:
		jmp 	(temp1)		

; *****************************************************************************
;
;					Stop a program running if assertion fails
;
; *****************************************************************************

AssertCode:	;; assert <0>
		lda 	TOS 						; TOS = 0
		ora 	TOS+1
		beq 	_ASFail 					; if zero fail
		popTOS 								; pop TOS off
		doNext
_ASFail:rerror	"ASSERT"
		
; *****************************************************************************
;
;							Stop a program running
;
; *****************************************************************************

StopCode:	;; stop <0>
		rerror	"STOP"

; *****************************************************************************
;
;						End a program (warm start)
;
; *****************************************************************************

EndProgram:	;; end <0>
		jmp 	WarmStart

; *****************************************************************************
;
;								Vocabulary List
;
; *****************************************************************************

VlistCode: ;; vlist <0>
		set16 	temp1,Dictionary 			; work down the dictionary
_VLLoop:ldy 	#1							; type byte
		lda 	(temp1),y
		ldy 	#4 							; Y = 4
		lsr 	a 							; shift bits 4 and 5 to 0,1
		lsr 	a
		lsr		a
		lsr 	a

		lsr 	a 							; add those bits in
		bcc 	_VLNoDec
		iny
		iny
_VLNoDec:		
		lsr 	a
		bcc 	_VLNoEnc
		iny
		iny
_VLNoEnc:		
		lda 	(temp1),y 					; remove $$ words
		cmp 	#"$"
		bne 	_VLPrint
		iny
		eor 	(temp1),y
		beq 	_VLNext
		dey
		;
_VLPrint:									; print name out
		lda 	(temp1),y
		pha
		iny
		and 	#$7F
		jsr 	ExternPrint
		pla
		asl 	a	
		bcc 	_VLPrint
		lda 	#32
		jsr 	ExternPrint
		;
_VLNext:		
		clc 								; go to next
		lda 	(temp1)	
		adc 	temp1
		sta 	temp1
		bcc 	_VLNoCarry
		inc 	temp1+1
_VLNoCarry:
		lda 	(temp1)
		bne 	_VLLoop
		lda 	#13
		jsr 	ExternPrint
		doNext

; *****************************************************************************
;
;									Dump the stack
;
; *****************************************************************************

DumpStack:	;; ..
		lda		#"["
		jsr 	ExternPrint
		stx 	temp1 						; X -> A
		tsx
		txa
		ldx 	temp1
		cmp 	#NumberStackBase 			; out of range, exit
		beq 	_DSExit
		pha 								; save it
		lda 	#COL_WHITE
		jsr 	ExternColour
		lda 	TOS 						; print TOS then stack values backwards
		ldy 	TOS+1
		sec 								; print unsigned.
		jsr 	PrintYA
		lda 	#COL_CYAN
		jsr 	ExternColour	
		ply 								; restore offset
_DSLoop:
		cpy 	#NumberStackBase-2 			; till done whole stack.
		beq 	_DSExit
		lda 	#" " 						; space
		jsr 	ExternPrint
		phy 								; save pos
		lda 	$0101,y 					; read stack item
		pha
		lda 	$0102,y		
		ply
		sec
		jsr 	PrintYA 					; print it
		ply 								; restore and advance pos
		iny
		iny
		bra 	_DSLoop
_DSExit:
		lda		#"]"
		jsr 	ExternPrint
		lda 	#13
		jsr 	ExternPrint
		doNext		
