; *****************************************************************************
; *****************************************************************************
;
;		Name :		skipper.src
;		Purpose :	$$comment $$define $$nextline
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		6th January 2020
;
; *****************************************************************************
; *****************************************************************************

; *****************************************************************************
;
;			Advance to next line. (IP) must point to the current line
;
; *****************************************************************************

NextLine: 	;; $$nextline hide <0>
		advance IP							; push IP to next line.
		lda 	(IP)						; is there any following program ?
		beq 	_NLGoEnd
		ldx 	#3 							; start 3 in
		jmp 	Next+2 						; avoid the first two INXs
_NLGoEnd:
		jmp 	WarmStart

; *****************************************************************************
;
;							Skip a comment
;
; *****************************************************************************

SkipComment: 	;; $$comment noexec <n> 
		inx									; (IP),X points to the length.
		inx 
		txa
		tay 								; put into Y
		clc
		adc 	(IP),y 						; add the length to X
		tax 								; put back in X
		jmp 	Next+2 						; continue skipping the pre-increment.

CommentDecoder: ;; $$comment decode
		lda 	#32
		jsr 	DecodeWriteBuffer
		lda 	#"'"
		jsr 	DecodeWriteBuffer
		lda 	#32
		jsr 	DecodeWriteBuffer
		jsr 	DecodeOutputData
		lda 	#32
		jsr 	DecodeWriteBuffer
		rts

; *****************************************************************************
;
;			If you run into a definition it generates an error.
;
; *****************************************************************************

DefineCode:	;; $$define noexec <n>
		rerror "DEFINITION?"

DefineDecoder: ;; $$define decode
		lda 	#32
		jsr 	DecodeWriteBuffer
		lda 	#":"
		jsr 	DecodeWriteBuffer
		jsr 	DecodeOutputData
		lda 	#32
		jsr 	DecodeWriteBuffer
		rts
