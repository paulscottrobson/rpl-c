; *****************************************************************************
; *****************************************************************************
;
;		Name :		branch.src
;		Purpose :	Branch routines.
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		8th January 2020
;
; *****************************************************************************
; *****************************************************************************

; *****************************************************************************
;
;						Branch if TOS = 0, removes TOS
;
; *****************************************************************************

BranchIfZero: 
		lda 	TOS 						; check TOS = 0
		ora 	TOS+1
		beq 	BranchTestSucceeds
		;
		;		Come here if branch test fails.
		;
BranchTestFails:		
		popTOS 								; drop
		inx 								; skip over the relative branch.
		jmp 	Next

; *****************************************************************************
;
;				 Unconditional Branch  with Drop
;
; *****************************************************************************

BranchTestSucceeds:
		popTOS 								; throw away the test value
		
BranchTestSucceedsNoPop:
		inx 								; advance by two, to the offset
		inx

BranchAlways:
		txa 								; A = Y = position
		tay 								; (IP),Y now points to the branch target
		;
		lda 	(IP),y 						; adjust the position 
		tax 								; position back in this line.
		jmp 	Next+2 						; and continue

		