; *****************************************************************************
; *****************************************************************************
;
;		Name :		memory.src
;		Purpose :	Memory Access words
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		6th January 2020
;
; *****************************************************************************
; *****************************************************************************

; *****************************************************************************
;
;									Byte Read
;
; *****************************************************************************

ByteRead: 	;; c@
		lda 	(TOS)
		sta 	TOS
		stz 	TOS+1
		jmp 	Next

; *****************************************************************************
;
;									Word Read
;
; *****************************************************************************

WordRead: 	;; @
		ldy 	#1
		lda 	(TOS),y
		tay
		lda 	(TOS)
		sta 	TOS
		sty 	TOS+1
		jmp 	Next

; *****************************************************************************
;
;									Byte Write
;
; *****************************************************************************

ByteWrite: 	;; c!
		pla
		pla	
		sta 	(TOS)	
		popTOS
		jmp 	Next

; *****************************************************************************
;
;									Word Write
;
; *****************************************************************************

WordWrite: 	;; !
		pla
		ldy 	#1
		sta 	(TOS),y
		pla
		sta 	(TOS)
		popTOS
		jmp 	Next

; *****************************************************************************
;
;									Word Add
;
; *****************************************************************************

WordAdd: ;; +!
		ply
		pla
		clc
		adc 	(TOS)
		sta 	(TOS)
		tya
		ldy 	#1
		adc 	(TOS),y
		sta 	(TOS),y
		popTOS
		jmp 	Next


